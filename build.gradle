import com.scherule.buildtools.ScheruleProjectVersion

buildscript {
    ext.kotlin_version = '1.1.3'
    ext.spring_version = '1.5.7.RELEASE'

    repositories {
        mavenLocal()
        maven { url "http://dl.bintray.com/kboom/scherule" }
        jcenter()
    }

    dependencies {
        classpath "com.scherule:scherule-buildtools:1.0.0"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M5'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.bmuschko:gradle-docker-plugin:3.0.8"
    }

}

repositories {
    mavenLocal()
    maven { url "http://dl.bintray.com/kboom/scherule" }
    mavenCentral()
}

apply plugin: 'com.scherule.project'
apply plugin: 'org.junit.platform.gradle.plugin'
apply from: "$projectDir/gradle/sourcesets.gradle"
apply from: "$projectDir/gradle/boot.gradle"
apply from: "$projectDir/gradle/container.gradle"

scheruleProject {

}

junitPlatform {
    platformVersion '1.0.0-M5'
    logManager 'java.util.logging.LogManager'
    enableStandardTestTask true

    filters {
        packages {
            include 'com.scherule.calendaring'
        }
    }
}

task e2eTest(
        type: JavaExec,
        description: 'Runs the e2e tests.',
        group: 'Verification'
) {
    dependsOn testClasses, startMyAppContainer, allocatePort
    shouldRunAfter test
    finalizedBy stopMyAppContainer

    classpath = sourceSets.e2e.runtimeClasspath

    main = 'org.junit.platform.console.ConsoleLauncher'
    jvmArgs = ["-Dhttp.port=${allocatePort.ext.port}"]
    args = ['--scan-class-path',
            sourceSets.e2e.output.getClassesDirs().asPath,
            '--reports-dir', "${buildDir}/test-results/junit-e2e"]
}

check {
    dependsOn e2eTest
}

dependencies {

    compile "com.scherule:scherule-commons:1.1.6"
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile group: 'com.rabbitmq', name: 'amqp-client', version: '4.1.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.0'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.9"
    compile group: 'org.ektorp', name: 'org.ektorp', version: '1.4.4'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'

    testCompile 'info.cukes:cucumber-guice:1.2.5'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.assertj:assertj-joda-time:2.0.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M5'
    testRuntime 'org.junit.platform:junit-platform-launcher:1.0.0-M5'
    testRuntime 'org.junit.platform:junit-platform-runner:1.0.0-M5'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M5'

    integrationCompile 'org.junit.platform:junit-platform-console:1.0.0-M5'

    e2eCompile 'com.google.inject:guice:4.1.0'
    e2eCompile 'com.google.inject.extensions:guice-multibindings:4.1.0'
    e2eCompile 'info.cukes:cucumber-guice:1.2.5'
    e2eCompile 'info.cukes:cucumber-java:1.2.5'
    e2eCompile 'info.cukes:cucumber-junit:1.2.5'
    e2eCompile 'org.assertj:assertj-core:3.8.0'
    e2eCompile 'org.assertj:assertj-joda-time:2.0.0'
    e2eCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M5'
    e2eCompile 'org.junit.platform:junit-platform-launcher:1.0.0-M5'
    e2eCompile 'org.junit.platform:junit-platform-runner:1.0.0-M5'
    e2eCompile 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M5'
    e2eCompile 'org.junit.platform:junit-platform-console:1.0.0-M5'
    e2eCompile 'io.rest-assured:rest-assured:3.0.3'
    e2eCompile 'io.rest-assured:json-path:3.0.3'
    e2eCompile 'io.rest-assured:json-schema-validator:3.0.3'
}